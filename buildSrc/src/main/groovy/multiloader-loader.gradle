plugins {
    id 'multiloader-common'
    id 'com.gradleup.shadow'
}

configurations {
    commonJava{
        canBeResolved = true
    }
    commonResources{
        canBeResolved = true
    }

    commonClientJava{
        canBeResolved = true
    }
    commonClientResources{
        canBeResolved = true
    }
}

dependencies {
    compileOnly(project(':common')) {
        capabilities {
            requireCapability "$group:$mod_id"
        }
        def loaderAttribute = Attribute.of('io.github.mcgradleconventions.loader', String)
        attributes {
            attribute(loaderAttribute, 'common')
        }
    }
    commonJava project(path: ':common', configuration: 'commonJava')
    commonResources project(path: ':common', configuration: 'commonResources')

    compileOnly(project(':common_client')) {
        capabilities {
            requireCapability "$group:$mod_id-client"
        }
        def loaderAttribute = Attribute.of('io.github.mcgradleconventions.loader', String)
        attributes {
            attribute(loaderAttribute, 'common_client')
        }
    }

    commonClientJava project(path: ':common_client', configuration: 'commonClientJava')
    commonClientResources project(path: ':common_client', configuration: 'commonClientResources')
}

tasks.named('compileJava', JavaCompile) {
    dependsOn(configurations.commonJava, configurations.commonClientJava)
    source(configurations.commonJava, configurations.commonClientJava)
}

processResources {
    dependsOn(configurations.commonResources)
    from(configurations.commonResources)
    dependsOn(configurations.commonClientResources)
//    from(configurations.commonClientResources)
    from(project(":common_client").sourceSets.main.resources)
}

tasks.named('javadoc', Javadoc).configure {
    dependsOn(configurations.commonJava, configurations.commonClientJava)
    source(configurations.commonJava, configurations.commonClientJava)
}

tasks.named('sourcesJar', Jar) {
    dependsOn(configurations.commonJava, configurations.commonClientJava)
    from(configurations.commonJava)
    from(configurations.commonClientJava)
    dependsOn(configurations.commonResources, configurations.commonClientResources)
    from(configurations.commonResources)
    from(configurations.commonClientResources)
}

shadowJar {
    archiveClassifier.set('')
    configurations = [project.configurations.shadow]

    addMultiReleaseAttribute = false

    relocate 'org.bouncycastle', 'ph.jldvmsrwll1a.authorisedkeysmc.vendor.bouncycastle'
    exclude 'META-INF/services/java.security.Provider'
    exclude 'META-INF/versions/**'

    exclude { details ->
        def path = details.path

        path.startsWith('org/bouncycastle/x509/') || path.startsWith('org/bouncycastle/pqc/')
    }

    zip64 true
    minimize()
}