plugins {
    id 'multiloader-common'
    id 'net.neoforged.moddev'
    id 'com.diffplug.spotless' version '8.1.0'
}

spotless {
    java {
        palantirJavaFormat()
    }
}

neoForge {
    neoFormVersion = neo_form_version
    // Automatically enable AccessTransformers if the file exists
    def at = file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
}

dependencies {
    compileOnly group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
    // fabric and neoforge both bundle mixinextras, so it is safe to use it in common
    compileOnly group: 'io.github.llamalad7', name: 'mixinextras-common', version: '0.3.5'
    annotationProcessor group: 'io.github.llamalad7', name: 'mixinextras-common', version: '0.3.5'

    // Source: https://mvnrepository.com/artifact/org.bouncycastle/bcprov-jdk18on
    compileOnly 'org.bouncycastle:bcprov-jdk18on:1.83'
    // Source: https://mvnrepository.com/artifact/org.bouncycastle/bcpkix-jdk18on
    compileOnly 'org.bouncycastle:bcpkix-jdk18on:1.83'

    compileOnly(project(':common')) {
        capabilities {
            requireCapability "$group:$mod_id"
        }
        def loaderAttribute = Attribute.of('io.github.mcgradleconventions.loader', String)
        attributes {
            attribute(loaderAttribute, 'common')
        }
    }
}

configurations {
    commonClientJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonClientResources {
        canBeResolved = false
        canBeConsumed = true
    }
}

artifacts {
    commonClientJava sourceSets.main.java.sourceDirectories.singleFile
    commonClientResources sourceSets.main.resources.sourceDirectories.singleFile
}

// Implement mcgradleconventions loader attribute
def loaderAttribute = Attribute.of('io.github.mcgradleconventions.loader', String)
['apiElements', 'runtimeElements', 'sourcesElements', 'javadocElements'].each { variant ->
    configurations.named("$variant") {
        attributes {
            attribute(loaderAttribute, 'common_client')
        }
    }
}
sourceSets.configureEach {
    [it.compileClasspathConfigurationName, it.runtimeClasspathConfigurationName].each { variant->
        configurations.named("$variant") {
            attributes {
                attribute(loaderAttribute, 'common_client')
            }
        }
    }
}

// Declare capabilities on the outgoing configurations.
// Read more about capabilities here: https://docs.gradle.org/current/userguide/component_capabilities.html#sec:declaring-additional-capabilities-for-a-local-component
['apiElements', 'runtimeElements', 'sourcesElements', 'javadocElements'].each { variant ->
    configurations."$variant".outgoing {
        capability("$group:${project.name}:$version")
        capability("$group:${base.archivesName.get()}:$version")
        capability("$group:$mod_id-${project.name}-client-${minecraft_version}:$version")
        capability("$group:$mod_id-client:$version")
    }
    publishing.publications.configureEach {
        suppressPomMetadataWarningsFor(variant)
    }
}
