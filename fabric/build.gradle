plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
    id 'com.gradleup.shadow' version '9.2.2'
    id 'com.diffplug.spotless' version '8.1.0'
}

spotless {
    setEnforceCheck(false)

    java {
        palantirJavaFormat()
    }
}

repositories {
    maven {
        name = 'TerraformersMC'
        url = uri('https://maven.terraformersmc.com/releases/')
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    modApi "com.terraformersmc:modmenu:${project.modmenu_version}"

    // Source: https://mvnrepository.com/artifact/org.bouncycastle/bcprov-jdk18on
    implementation "org.bouncycastle:bcprov-jdk18on:${bouncycastle_version}"
    shadow "org.bouncycastle:bcprov-jdk18on:${bouncycastle_version}"
}

loom {
    def aw = project(':fabric').file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName('Fabric Client')
            ideConfigGenerated(true)
            runDir('runs/client')
        }
        server {
            server()
            setConfigName('Fabric Server')
            ideConfigGenerated(true)
            runDir('runs/server')
        }
    }
}

// Implement mcgradleconventions loader attribute
def loaderAttribute = Attribute.of('io.github.mcgradleconventions.loader', String)
['apiElements', 'runtimeElements', 'sourcesElements', 'javadocElements', 'includeInternal', 'modCompileClasspath'].each { variant ->
    configurations.named("$variant") {
        attributes {
            attribute(loaderAttribute, 'fabric')
        }
    }
}
sourceSets.configureEach {
    [it.compileClasspathConfigurationName, it.runtimeClasspathConfigurationName].each { variant->
        configurations.named("$variant") {
            attributes {
                attribute(loaderAttribute, 'fabric')
            }
        }
    }
}
loom.remapConfigurations.configureEach {
    configurations.named(it.name) {
        attributes {
            attribute(loaderAttribute, 'fabric')
        }
    }
}

shadowJar {
    archiveClassifier.set('shadow')
    configurations = [project.configurations.shadow]

    addMultiReleaseAttribute = false

    relocate 'org.bouncycastle', 'ph.jldvmsrwll1a.authorisedkeysmc.vendor.bouncycastle'
    exclude 'META-INF/services/java.security.Provider'
    exclude 'META-INF/versions/**'

    zip64 true
    minimize()
}


remapJar {
    dependsOn 'shadowJar'

    inputFile.set(shadowJar.archiveFile.get())
}